plugins {
  id 'java'
  id 'application'
  id 'idea'

  // Utilizes google-java-format to format the Java source files
  // See https://github.com/sherter/google-java-format-gradle-plugin
  id 'com.github.sherter.google-java-format' version '0.9'

  // see https://docs.freefair.io/gradle-plugins/5.1.0/reference/
  id "io.freefair.lombok" version "5.1.0"

  // Assembles your modules into a custom runtime image
  // See https://plugins.gradle.org/plugin/org.beryx.runtime
  id 'org.beryx.runtime' version '1.9.1'
}

repositories {
  jcenter()
}

group = "io.github.koppor"
version = '0.1.0-dev'

java {
  sourceCompatibility = JavaVersion.VERSION_13
  targetCompatibility = JavaVersion.VERSION_13
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

dependencies {

  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

  implementation 'commons-io:commons-io:2.7'


  implementation 'com.google.code.gson:gson:2.8.6'

  implementation 'org.tinylog:tinylog-api:2.1.1'
  implementation 'org.tinylog:tinylog-impl:2.1.1'

  implementation 'me.tongfei:progressbar:0.8.1'

  implementation 'org.apache.commons:commons-text:1.8'

  implementation 'org.jgrapht:jgrapht-core:1.4.0'

  // https://picocli.info/
  implementation 'info.picocli:picocli:4.2.0'
  // required for GraalVM native image
  annotationProcessor 'info.picocli:picocli-codegen:4.2.0'

  // https://github.com/google/jimfs
  testImplementation 'com.google.jimfs:jimfs:1.1'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

application {
  mainClassName = 'io.github.koppor.kodf.App'
}

test {
  useJUnitPlatform()
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

javadoc {
  options {
    encoding = 'UTF-8'
    version = true
    author = true
  }
}

runtime {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

compileJava {
  // enable the annotation processor - required by picoli and GraalVM native image
  options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
